plugins {
    id 'java'
    id 'maven-publish'
}

group = 'c0rnell.flexer'
version = '1.0.0'

java {
    sourceCompatibility = '8'
    targetCompatibility = '8'
}

jar {
    // excluding all sources in the package starting with org
    // it's needed to remove some classes from resulting jar like QueryRewriter
    // which is used to successfully assemble the library and for nothing else
    exclude('org')

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes["Premain-Class"] = "c0rnell.flexer.asm.FlexerInstrumentationAgent"
        attributes["Agent-Class"] = 'c0rnell.flexer.asm.FlexerInstrumentationAgent'
        attributes["Can-Redefine-Classes"] = true
        attributes["Can-Retransform-Classes"] = true
        attributes["Set-Native-Method-Prefix"] = true
        attributes["Implementation-Title"] = "ConditionalQueryInstrumentation"
        attributes["Implementation-Version"] = version
    }
    from {
        configurations.runtimeClasspath
                .findAll { it.name.endsWith('jar') }
                .collect { it.isDirectory() ? it : zipTree(it) }
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

dependencies {
    compileOnly files("../lib/tools.jar")
    implementation 'org.ow2.asm:asm:9.6'
    implementation 'org.ow2.asm:asm-commons:9.6'

    implementation 'net.bytebuddy:byte-buddy-agent:1.14.5'
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'flexer-agent'
            version = version

            from components.java
        }
    }
}

